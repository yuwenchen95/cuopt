/* clang-format off */
/*
 * SPDX-FileCopyrightText: Copyright (c) 2025-2026, NVIDIA CORPORATION & AFFILIATES. All rights reserved.
 * SPDX-License-Identifier: Apache-2.0
 */
/* clang-format on */

#pragma once

#include <cub/cub.cuh>

#include <raft/core/copy.hpp>
#include <raft/core/device_span.hpp>
#include <raft/core/host_span.hpp>
#include <raft/util/cuda_rt_essentials.hpp>

#include <rmm/device_buffer.hpp>
#include <rmm/device_scalar.hpp>
#include <rmm/device_uvector.hpp>
#include <rmm/exec_policy.hpp>

#include <thrust/execution_policy.h>
#include <thrust/functional.h>
#include <thrust/transform_reduce.h>

#include <cmath>
#include <vector>
namespace cuopt::mathematical_optimization {

struct norm_inf_max {
  template <typename f_t>
  __device__ __forceinline__ f_t operator()(const f_t& a, const f_t& b) const
  {
    f_t x = cuda::std::abs(a);
    f_t y = cuda::std::abs(b);
    return x > y ? x : y;
  }
};

template <typename i_t, typename f_t, typename InputIteratorT>
f_t device_custom_vector_norm_inf(InputIteratorT in, i_t size, rmm::cuda_stream_view stream_view)
{
  if (size == 0) { return 0; }
  // FIXME: Tmp storage stored in vector_math class.
  auto d_out = rmm::device_scalar<f_t>(stream_view);
  rmm::device_uvector<uint8_t> d_temp_storage(0, stream_view);
  size_t temp_storage_bytes = 0;
  f_t init                  = 0;
  auto custom_op            = norm_inf_max{};
  cub::DeviceReduce::Reduce(d_temp_storage.data(),
                            temp_storage_bytes,
                            in,
                            d_out.data(),
                            size,
                            custom_op,
                            init,
                            stream_view);

  d_temp_storage.resize(temp_storage_bytes, stream_view);

  cub::DeviceReduce::Reduce(d_temp_storage.data(),
                            temp_storage_bytes,
                            in,
                            d_out.data(),
                            size,
                            custom_op,
                            init,
                            stream_view);
  return d_out.value(stream_view);
}

// Same reduction as device_custom_vector_norm_inf, but writes into a caller-supplied device
// pointer (and reuses a caller-supplied temp-storage buffer) instead of allocating a private
// rmm::device_scalar and blocking on .value(). Lets callers batch several reductions and defer
// the host readback to a single copy + sync.
template <typename i_t, typename f_t, typename InputIteratorT>
void enqueue_norm_inf_into(InputIteratorT in,
                           i_t size,
                           f_t* out,
                           rmm::device_buffer& tmp,
                           rmm::cuda_stream_view stream_view)
{
  if (size == 0) {
    RAFT_CUDA_TRY(cudaMemsetAsync(out, 0, sizeof(f_t), stream_view.value()));
    return;
  }
  size_t temp_storage_bytes = 0;
  f_t init                  = 0;
  auto custom_op            = norm_inf_max{};
  cub::DeviceReduce::Reduce(
    nullptr, temp_storage_bytes, in, out, size, custom_op, init, stream_view);

  tmp.resize(temp_storage_bytes, stream_view);

  cub::DeviceReduce::Reduce(
    tmp.data(), temp_storage_bytes, in, out, size, custom_op, init, stream_view);
}

// Sum reduction into a caller-supplied device pointer/temp-storage buffer, deferring the host
// readback (see enqueue_norm_inf_into).
template <typename i_t, typename f_t, typename InputIteratorT>
void enqueue_sum_into(InputIteratorT in,
                      i_t size,
                      f_t* out,
                      rmm::device_buffer& tmp,
                      rmm::cuda_stream_view stream_view)
{
  size_t temp_storage_bytes = 0;
  cub::DeviceReduce::Sum(nullptr, temp_storage_bytes, in, out, size, stream_view);

  tmp.resize(temp_storage_bytes, stream_view);

  cub::DeviceReduce::Sum(tmp.data(), temp_storage_bytes, in, out, size, stream_view);
}

// Max reduction (with a floor of 0, matching this codebase's existing
// thrust::reduce(..., f_t(0), thrust::maximum<f_t>()) usage) into a caller-supplied device
// pointer/temp-storage buffer, deferring the host readback (see enqueue_norm_inf_into).
template <typename i_t, typename f_t, typename InputIteratorT>
void enqueue_max_into(InputIteratorT in,
                      i_t size,
                      f_t* out,
                      rmm::device_buffer& tmp,
                      rmm::cuda_stream_view stream_view)
{
  size_t temp_storage_bytes = 0;
  f_t init                  = 0;
  auto custom_op            = thrust::maximum<f_t>{};
  cub::DeviceReduce::Reduce(
    nullptr, temp_storage_bytes, in, out, size, custom_op, init, stream_view);

  tmp.resize(temp_storage_bytes, stream_view);

  cub::DeviceReduce::Reduce(
    tmp.data(), temp_storage_bytes, in, out, size, custom_op, init, stream_view);
}

template <typename i_t, typename f_t>
f_t device_vector_norm_inf(const rmm::device_uvector<f_t>& in, rmm::cuda_stream_view stream_view)
{
  return device_custom_vector_norm_inf<i_t, f_t>(in.data(), in.size(), stream_view);
}

template <typename i_t, typename f_t>
f_t device_vector_norm_inf(raft::device_span<const f_t> in, rmm::cuda_stream_view stream_view)
{
  return device_custom_vector_norm_inf<i_t, f_t>(in.data(), in.size(), stream_view);
}

// TMP we should just have a CPU and GPU version to do the comparison
// Should never have to norm inf a CPU vector if we are using the GPU
template <typename i_t, typename f_t, typename Allocator>
f_t vector_norm_inf(const std::vector<f_t, Allocator>& x, rmm::cuda_stream_view stream_view)
{
  const auto d_x = device_copy(x, stream_view);
  return device_vector_norm_inf<i_t, f_t>(d_x, stream_view);
}

template <typename i_t, typename f_t>
f_t vector_norm_inf(raft::host_span<const f_t> x, rmm::cuda_stream_view stream_view)
{
  rmm::device_uvector<f_t> d_x(x.size(), stream_view);
  raft::copy(d_x.data(), x.data(), x.size(), stream_view);
  return device_vector_norm_inf<i_t, f_t>(d_x, stream_view);
}

template <typename f_t>
f_t vector_norm_inf(const rmm::device_uvector<f_t>& x)
{
  auto begin   = x.data();
  auto end     = x.data() + x.size();
  auto max_abs = thrust::transform_reduce(
    rmm::exec_policy(x.stream()),
    begin,
    end,
    [] __host__ __device__(f_t val) { return abs(val); },
    static_cast<f_t>(0),
    thrust::maximum<f_t>{});
  RAFT_CHECK_CUDA(x.stream());
  return max_abs;
}

template <typename f_t>
f_t vector_norm2(const rmm::device_uvector<f_t>& x)
{
  auto begin          = x.data();
  auto end            = x.data() + x.size();
  auto sum_of_squares = thrust::transform_reduce(
    rmm::exec_policy(x.stream()),
    begin,
    end,
    [] __host__ __device__(f_t val) { return val * val; },
    f_t(0),
    thrust::plus<f_t>{});
  RAFT_CHECK_CUDA(x.stream());
  return std::sqrt(sum_of_squares);
}

}  // namespace cuopt::mathematical_optimization
